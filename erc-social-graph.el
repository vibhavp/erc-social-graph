;;; erc-social-graph.el --- A social network graph module for ERC.

;; Copyright (C) 2014 Vibhav Pant <vibhavp@gmail.com>

;; Url: https://github.com/vibhavp/erc-social-graph
;; Author: Vibhav Pant <vibhavp@gmail.com>
;; Version: 1.0
;; Keywords: erc graph

;;; Commentary:
;; erc-social-graph scans user received messages, and generates a social graph
;; of the channel. These graphs can be later converted to a DOT graph, which
;; can be drawn into a graph using Graphviz.

;;; License:

;; This program is free software: you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation, either version 3 of the License, or
;; (at your option) any later version.

;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
;; GNU General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with this program. If not, see <http://www.gnu.org/licenses/>.
;; This file is not a part of GNU Emacs.
;;; Code:
(require 'erc)

(defcustom erc-social-graph-update-function 'erc-social-graph-update-graph
  "The function used for updating the graph hash table.
 Takes the message sender as an argument."
  :type 'function
  :group 'erc-social-graph)

(defcustom erc-social-graph-dynamic-graph nil
  "Update the DOT graph buffer on every sent message."
  :type 'boolean
  :group 'erc-social-graph)

(defvar erc-social-graph-table (make-hash-table :test 'equal))

(defun erc-social-graph-update-graph (sender text)
  "Update the graph"
  (maphash (lambda (nick value)
	     (let ((result (ignore-errors (string-match
					   (format "\\<%s\\>\\([^[:alpha:]]\\|$\\)"
						   (downcase nick))
					   (downcase text)))))
	       (when result
		 (let* ((key (concat (downcase sender) "-" (downcase nick)))
			(times (gethash key (gethash (buffer-name)
						     erc-social-graph-table)
					nil)))
		   (if (eq times nil)
		       (puthash key (setq times 1) (gethash (buffer-name)
						erc-social-graph-table))
		     (puthash key (incf times) (gethash (buffer-name)
							  erc-social-graph-table)))
		   (when erc-social-graph-dynamic-graph
		     (erc-social-graph-update-dot-buffer (buffer-name) sender nick
							 times))))))
	   erc-channel-users))

(defun erc-social-graph-update ()
  "Check if the text sent in the current buffer is a user sent message,
 and accordingly pass it to erc-social-graph-update-function"
  (let ((text (buffer-substring-no-properties (point-min) (point-max))))    
    (when (string=  (substring text 0 1) "<")
      (let ((sender (substring text (+ 1 (string-match "<" text))
				(string-match ">" text))))
	(setq text (substring text (+ 2 (string-match ">" text))))
	(funcall erc-social-graph-update-function sender text)))))

(defun erc-social-graph-create ()
  "Create empty graph for current-buffer"
  (puthash (buffer-name) (make-hash-table :test 'equal) erc-social-graph-table))

(defun erc-social-graph-update-dot-buffer (channel nick1 nick2 mentions)
  "Update DOT buffer for channel, set \"NICK1 -> NICK2 mentions\" to MENTIONS"
  (let ((buffer (format "erc-social-graph-%s.dot" channel)))
    (if (bufferp (get-buffer buffer))
	(with-current-buffer buffer
	  (goto-char (point-min))
	  (if (re-search-forward (format
				  "\"%s\" -> \"%s\" \\[penwidth = [[:digit:]]+\\]"
				  nick1
				  nick2))
	      (replace-match (format "\"%s\" -> \"%s\" [penwidth = %d]"
				     nick1
				     nick2
				     (if (> mentions 6)
					 6
				       mentions)))
	    ;; Create new edge/node(s)
	    (goto-char (- (point-max) 2))
	    (insert (format "\"%s\" -> \"%s\" [penwidth = %d]; \n"
			    nick1 nick2
			    (if (> mentions 6)
				6
			      mentions)))))
      ;; Create new file
      (with-current-buffer (get-buffer-create buffer)
	(insert (format "//Generated automatically. Do not modify.
digraph {
labelloc=\"t\";
label=\"Generated by erc-social-graph using %s\";
\"%s\" -> \"%s\" [penwidth = %d];\n
}" (erc-version) nick1 nick2 mentions))))))

(defun erc-social-graph-draw (channel)
  "Draw graph for CHANNEl"
  (interactive (list (completing-read "Draw graph for channel: "
				      (mapcar 'buffer-name (erc-buffer-list)))))
  (let ((buffer (format "erc-social-graph-%s.dot" channel))
	(channel-graph (gethash channel erc-social-graph-table)))
    (unless (bufferp buffer)
      (with-current-buffer (get-buffer-create buffer)
	(insert (format "digraph {
labelloc=\"t\";
label=\"Generated by erc-social-graph using %s\";\n" (erc-version)))
	(maphash (lambda (link value)
		   (insert (format "\"%s\" -> \"%s\" [penwidth = %d];\n"
				   (substring link 0 (string-match "-" link))
				   (substring link (+ 1 (string-match "-"
								      link)))
				   (if (> value 4)
				       6
				     value))))
		 channel-graph)
	(insert "}")))
    (switch-to-buffer buffer)))

(define-erc-module social-graph nil
  "Social network graphs for emacs"
  ;; Enable
  ((add-hook 'erc-join-hook 'erc-social-graph-create)
   (add-hook 'erc-insert-post-hook 'erc-social-graph-update)
   (add-hook 'erc-send-post-hook 'erc-social-graph-update))
  ;; Disable
  ((remove-hook 'erc-insert-post-hook 'erc-social-graph-update)
   (remove-hook 'erc-join-hook 'erc-social-graph-create)
   (remove-hook 'erc-send-post-hook 'erc-social-graph-update)))

(provide 'erc-social-graph)
